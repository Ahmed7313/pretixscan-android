apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.sentry.android.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "eu.pretix.pretixscan.droid"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 5
        versionName "0.1.4"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        disable 'InvalidPackage' // problem with jetty and bouncycastle
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
    }

    buildTypes {
		def sentry_dsn = "null"
        def sumup_key = "null"
		try {
			Properties properties = new Properties()
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
            sentry_dsn = properties.getProperty('sentry.dsn')
            if (sentry_dsn == null) {
                sentry_dsn = "null"
            } else {
                sentry_dsn = "\"" + sentry_dsn + "\""
            }
			sumup_key = properties.getProperty('sumup.key')
			if (sumup_key == null) {
                sumup_key = "null"
			} else {
                sumup_key = "\"" + sumup_key + "\""
			}
		} catch (all) {
		}

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "String", "SENTRY_DSN", sentry_dsn
            buildConfigField "String", "SUMUP_AFFILIATE_KEY", sumup_key
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "SENTRY_DSN", "null"
            buildConfigField "String", "SUMUP_AFFILIATE_KEY", sumup_key
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

sentry {
    autoProguardConfig true
    autoUpload true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains.anko:anko:0.10.6'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.paging:paging-runtime:2.1.0'

    implementation 'com.github.kizitonwose.time:time-android:1.0.1'
    implementation 'com.github.traex.rippleeffect:library:1.3'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'

    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'io.sentry:sentry-android:1.7.16'
    implementation 'org.slf4j:slf4j-nop:1.7.25'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'io.requery:requery:1.5.1'
    implementation 'io.requery:requery-android:1.5.1'
    implementation 'io.requery:requery-kotlin:1.5.1'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.9'

    annotationProcessor 'io.requery:requery-processor:1.5.1'
    implementation(project(':libpretixsync')) {
        transitive = false
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
kapt {
    generateStubs = true
}